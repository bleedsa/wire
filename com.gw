ROOT:"/srv/wire"

trimL:{+/{" "~*x}{1_x}/-1$x}

whoami:{[db]
        c:!/+(trimL@url.dec@)''("="\)'";"\env "HTTP_COOKIE"
        hash:c "sesh"
        sesh:*sql.qry[db;"select * from sessions where hash = '$hash'"]
        me:?[~0=#sesh
             *sql.qry[db;"select * from auth where id = "+$sesh "user"]
             0]
        me}

boards:{[db]sql.qry[db;"select * from boards where not hidden"]}

page:{[db;title;body]
      v:read ROOT+"/version.txt"
      / you or false
      u:.[whoami;db;{[x]0}]
      / a link to your profile if you're logged in
      n:?[u;"[<a href=\"/user.php?u="+u["name"]+"\">"+u["name"]+"</a>]";""]
      / boards
      B:" "/{n:x"name";"<a href=\"/board.gw?b=$n\">$n</a>"}'boards db
      qq`
      <!DOCTYPE html>
      <html>
          <head>
              <meta charset="utf-8">
              <title>$title - the wire</title>
              <link rel="stylesheet" type="text/css" href="/css/theme.php">
          </head>
          <body>
              <div class="main">
                  <span class="top">
                      <b><a href="/">the wire</a></b>
                      | $n
                        <a href="/login.php">login</a>
                        <a href="/register.php">register</a>
                      | $B
                  </span>
                  $body
                  <br>
                  <span class="top">$v</span>
              </div>
          </body>
          <script src="/inc/index.js"></script>
      </html>`}

FORM:read STDIN
POST:!/+url.dec''("="\)'"&"\FORM
GET:.[{!/+url.dec''("="\)'"&"\env x};"QUERY_STRING";{[x]()!()}]
